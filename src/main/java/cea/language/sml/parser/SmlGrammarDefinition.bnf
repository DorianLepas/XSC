{
  parserClass="cea.language.sml.parser.SmlParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Sml"
  psiImplClassSuffix="Impl"
  psiPackage="cea.language.sml.psi"
  psiImplPackage="cea.language.sml.psi.impl"

  elementTypeHolderClass="cea.language.sml.psi.SmlTypes"
  elementTypeClass="cea.language.sml.psi.SmlElementType"
  tokenTypeClass="cea.language.sml.psi.SmlTokenType"

  psiImplUtilClass="cea.language.sml.psi.impl.SmlPsiImplUtil"

  pin=1
}

smlFile ::= smlFileContent*

private smlFileContent ::= (COMMENT|LINE_END|stateBlock|aliasBlock|alarmBlock|eventBlock|traceBlock|scriptBlock)

/* Règles grammaticales d'un état */
stateBlock ::= STATE separator stateName beginBlock stateBody* endBlock

private stateBody ::= stateBlock|aliasBlock|alarmBlock|eventBlock|traceBlock|enterBlock|exitBlock|optionsBlock


/* Règles grammaticales d'un alias */
aliasBlock ::= ALIAS separator aliasName beginBlock aliasBlockBody endBlock

private aliasBlockBody ::= EVENT_NAME+ {name="Events names"}


/* Règles grammaticales d'une alarm */
alarmBlock ::= ALARM separator eventsDefinition beginBlock instructions* endBlock


/* Règles grammaticales d'un événement */
eventBlock ::= EVENT separator eventsDefinition beginBlock instructions* endBlock


/* Règles grammaticales d'une trace */
traceBlock ::= TRACE separator eventsDefinition beginBlock instructions* endBlock


/* Règles grammaticales d'un script */
scriptBlock ::= SCRIPT beginBlock bindingBlock javascript? endBlock

bindingBlock ::= BINDING beginBlock bind+ endBlock

bind ::= BIND_NAME EQUALS_SEPARATOR smlVars {name="binds"}

javascript ::= JAVASCRIPT_CODE+

/* Règles grammaticales d'un bloc enter */
enterBlock ::= ENTER beginBlock instructions* endBlock


/* Règles grammaticales d'un bloc exit */
exitBlock ::= EXIT beginBlock instructions* endBlock


/* Règles grammaticales d'un bloc option */
optionsBlock ::= OPTION beginBlock OPTIONS* endBlock


/* Règle grammaticales d'un bloc condition */
conditionBlock ::= CONDITION beginBlock conditions* endBlock

/* Règles des blocs if, else if ou else */
ifBlock ::= IF ifConditions beginBlock instructions* endBlock

elseIfBlock ::= ELSE_IF ifConditions beginBlock instructions* endBlock

elseBlock ::= ELSE beginBlock instructions* endBlock


/* Règle des définitions d'événements */
eventsDefinition ::= eventsValue (EVENT_NAME_SEPARATOR eventsValue)* | ALL_EVENTS {name="Events definition"}
eventsValue ::= EVENT_NAME {name="Events Value" mixin="cea.language.sml.psi.impl.SmlNamedElementImpl"
                                                implements="cea.language.sml.psi.SmlNamedElement"
                                                methods=[getKey getValue getName setName getNameIdentifier]}


/* Règle des conditions pour un bloc de condition */
conditions ::= (THREAD_KEYWORD|PROCESS_KEYWORD|SML_VARS|CONST_CONDS) ((OP_CONDS|COMP_CONDS) (THREAD_KEYWORD|PROCESS_KEYWORD|SML_VARS|CONST_CONDS))*

/* Règle des conditions pour un bloc if, else if ou else */
ifConditions ::= BEGIN_PARENTHESE (((THREAD_KEYWORD|PROCESS_KEYWORD|SML_VARS|CONST_CONDS) ((OP_CONDS|COMP_CONDS) (THREAD_KEYWORD|PROCESS_KEYWORD|SML_VARS|CONST_CONDS))*) | JAVA_FUNCTION_CALL) END_PARENTHESE?


/* Règles grammaticales des instructions */
private instructions ::= ifBlock|elseIfBlock|elseBlock|conditionBlock|scriptBlock
|(threadStateInstruction
|callJavaFunctionInstruction
|gotoStateInstruction
|processStateInstruction
|threadEndInstruction
|traceInstruction
|execEndInstruction
|consumeEventInstruction
|setInstruction) {name="Instructions"}

// Instruction goto state
gotoStateInstruction ::= GOTO_STATE SEPARATOR stateName (STATE_NAME_SEPARATOR stateName)*

// Instruction process state
processStateInstruction ::= PROCESS_STATE SEPARATOR stateName identificationKey

// Instrution call
callJavaFunctionInstruction ::= CALL JAVA_FUNCTION_CALL {name="Call Java Function" mixin="cea.language.sml.psi.impl.SmlNamedElementImpl"
                                                                                   implements="cea.language.sml.psi.SmlNamedElement"
                                                                                    methods=[getKey getValue getParametersList getParametersCount getName setName getNameIdentifier]}

// Instruction thread_end
threadEndInstruction ::= THREAD_END

// Instruction thread_state
threadStateInstruction ::= THREAD_STATE SEPARATOR THREAD_NAME identificationKey

// Instruction exec_end
execEndInstruction ::= EXEC_END

// Instruction consume_event
consumeEventInstruction ::= CONSUME_EVENT

setInstruction ::= SET setProperty EQUALS_SEPARATOR setValue

setProperty ::= (smlVars DOT_SEPARATOR? smlVars?) | (((THREAD_KEYWORD|PROCESS_KEYWORD) identificationKey) DOT_SEPARATOR smlVars)

setValue ::= (smlVars|THREAD_KEYWORD|PROCESS_KEYWORD)

// Instruction MESSAGE, WARNING ou DEBUG
traceInstruction ::= (MESSAGE|WARNING|DEBUG) traceMessage?

traceMessage ::= (JAVA_FUNCTION_CALL|TRACE_MESSAGE_STRING|SML_VARS|THREAD_KEYWORD|PROCESS_KEYWORD) (TRACE_MESSAGE_SEPARATOR (TRACE_MESSAGE_STRING|SML_VARS|THREAD_KEYWORD|PROCESS_KEYWORD|JAVA_FUNCTION_CALL))*

/* Les règles suivantes ne contiennent qu'un seul token avec le message d'erreur
   associé à ce token lorsque celui-ci est manquant */
private separator ::= SEPARATOR {name="Separator ':' "}

private beginBlock ::= BEGIN_BLOCK {name="Begin block '{' "}

private endBlock ::= END_BLOCK {name="End block '}' "}

private stateName ::= STATE_NAME {name="State name"}

private aliasName ::= ALIAS_NAME {name="Alias name"}

private identificationKey ::= IDENTIFICATION_KEY {name="Identification Key"}

private binds ::= BINDS {name="Bindings"}

private smlVars ::= SML_VARS {name="Variables"}
